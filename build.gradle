plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "java-library"
    id "maven-publish"
    id 'signing'
}

group 'com.xiachufang'
version '10.1.13'


java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        url "https://packages.confluent.io/maven/"
    }
    maven {
        url "https://maven.aliyun.com/repository/spring"
    }
}

dependencies {
    implementation 'io.confluent:kafka-connect-hdfs:10.1.13'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

shadowJar {
    classifier = ''
    zip64 true
}

configurations {
    fatJarRuntimeElements {
        canBeResolved = false
        canBeConsumed = true
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, LibraryElements.JAR))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EMBEDDED)) // means no relocation of packages
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 8)
        }
        outgoing.artifact(shadowJar)
    }
}

components.java {
    addVariantsFromConfiguration(configurations.fatJarRuntimeElements) {
        mapToOptional()
    }
}

publishing {
    publications{
        mavenJava(MavenPublication) {
            artifactId = 'kafka-connect-hdfs-bundle'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'kafka connect hdfs bundle jar(fat jar)'
                description = 'kafka connect hdfs bundle jar(fat jar)'
                url = 'http://github.com/xiachufang/kafka-connect-hdfs-bundle'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'cadl'
                        name = 'liu jialin'
                        email = 'ctrlaltdeleteliu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/xiachufang/kafka-connect-hdfs-bundle.git'
                    developerConnection = 'scm:git:ssh://github.com/xiachufang/kafka-connect-hdfs-bundle.git'
                    url = 'http://github.com/xiachufang/kafka-connect-hdfs-bundle'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
